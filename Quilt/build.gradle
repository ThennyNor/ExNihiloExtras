import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'org.quiltmc.loom' version '1.1.+'
    id 'idea'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${mod_id}-quilt-${quilt_supported_versions}"

dependencies {
    minecraft "com.mojang:minecraft:${quilt_minecraft_version}"
    mappings "net.fabricmc:yarn:${fabric_yarn_mappings}:v2"
    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"
    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_version}-${minecraft_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    // JEI
    if (project.hasProperty('jei_version')) {
        modCompileOnly("mezz.jei:jei-${fabric_minecraft_version}-common-api:${jei_version}")
        modCompileOnly("mezz.jei:jei-${fabric_minecraft_version}-fabric-api:${jei_version}")
//        modRuntimeOnly("mezz.jei:jei-${fabric_minecraft_version}-fabric:${jei_version}")
    }

    // CONFIGURED
    if (project.hasProperty('fabric_configured_version')) {
        modImplementation "curse.maven:configured-fabric-667378:${fabric_configured_version}"
    }

    // CATALOGUE
    if (project.hasProperty('fabric_catalogue_version')) {
        modImplementation("curse.maven:catalogue-667377:${fabric_catalogue_version}")
        if (project.hasProperty('fabric_menulogue_version')) {
            modImplementation "curse.maven:menulogue-682371:${fabric_menulogue_version}"
        }
    }

    modImplementation "curse.maven:fabricae-ex-nihilo-596911:4146052" //https://www.curseforge.com/minecraft/mc-mods/fabricae-ex-nihilo/files?page=1&pageSize=20
    modImplementation "curse.maven:cloth-config-348521:4633414" //https://www.curseforge.com/minecraft/mc-mods/cloth-config/files?page=1&pageSize=20

}

loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../../API Tokens/exnihiloextras.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${quilt_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    mainFile.addRequirement("qsl")
    mainFile.addRequirement("fabricae-ex-nihilo")
    mainFile.addOptional("catalogue-fabric")
}

modrinth {
    token = file('../../API Tokens/modrinth.md').text
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${quilt_release_versions}".split(",") as List<String>
    dependencies {
        required.project "qsl"
        required.project "fabricae-ex-nihilo"
    }
}
